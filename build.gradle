buildscript {
    apply from: 'lib.gradle'

    repositories {
        google()
        mavenCentral()
        maven { url "https://s01.oss.sonatype.org/content/repositories/releases/" }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.1'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.21'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }

    afterEvaluate {

        if (!project.hasProperty("android")) return

        android {
            compileSdkVersion application.compileSdk
            buildToolsVersion application.buildTools

            defaultConfig {
                minSdkVersion application.minSdk
                targetSdkVersion application.targetSdk
                versionCode application.versionCode
                versionName application.versionName

                testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                consumerProguardFiles "consumer-rules.pro"
            }

            buildTypes {
                release {
                    minifyEnabled false
                    proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                }
            }

            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }

            kotlinOptions {
                jvmTarget = JavaVersion.VERSION_1_8.toString()
            }

            sourceSets.forEach { sourceSet ->
                sourceSet.jniLibs.srcDirs = ['libs', "libs/${sourceSet.name}"]
                sourceSet.java.srcDirs += "src/${sourceSet.name}/kotlin"
                sourceSet.assets.srcDirs += "src/${sourceSet.name}/assets"
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}