plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'signing'
}

group = "io.github.pedro-bachiega"
version = application.versionName

publishing {
    repositories {
        maven {
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
            name = "maven"
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }

    publications {
        betterSeekbar(MavenPublication) {
            artifactId = "better-seekbar"

            pom {
                name = "Better SeekBar"
                description = "A better and customizable version of SeekBar for Android apps"
                url = "https://github.com/Pedro-Bachiega/better-seekbar"

                scm {
                    url = "https://github.com/Pedro-Bachiega/better-seekbar"
                    connection = "scm:https://github.com/Pedro-Bachiega/better-seekbar.git"
                    developerConnection = "scm:git@github.com:Pedro-Bachiega/better-seekbar.git"
                }

                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                developers {
                    developer {
                        id = developerId
                        name = developerName
                        email = developerEmail
                    }
                }
            }
        }
    }
}

android {
    namespace 'com.pedrobneto.bar'

    task sourcesJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
        classifier = 'sources'
    }

    task javadoc(type: Javadoc) {
        excludes = ['**/*.kt']
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
        options.addStringOption('encoding', 'UTF-8')
    }

    artifacts {
        archives javadocJar, sourcesJar
    }
}

signing {
    sign publishing.publications.betterSeekbar
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.6.21'
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
}