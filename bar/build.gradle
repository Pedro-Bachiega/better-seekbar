plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven'
    id 'signing'
}

group = "io.github.pedro-bachiega"
archivesBaseName = "curved-seek-bar"
version = application.versionName

signing {
    sign configurations.archives
}

android {
    task sourcesJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
        classifier = 'sources'
    }

    task javadoc(type: Javadoc) {
        excludes = ['**/*.kt']
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
        options.addStringOption('encoding', 'UTF-8')
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'Curved Seek Bar'
                    packaging 'jar'
                    description 'Curved SeekBar for Android apps based on Bezier\'s cubic curve :)'
                    url 'https://github.com/Pedro-Bachiega/curved-seekbar'

                    scm {
                        url 'https://github.com/Pedro-Bachiega/curved-seekbar'
                        connection "scm:https://github.com/Pedro-Bachiega/curved-seekbar.git"
                        developerConnection 'scm:git@github.com:Pedro-Bachiega/curved-seekbar.git'
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id developerId
                            name developerName
                            email developerEmail
                        }
                    }
                }
            }
        }
    }
}

dependencies {
    implementation dependency_versions.kotlin
    implementation dependency_versions.coreKtx
    implementation dependency_versions.appCompat
}